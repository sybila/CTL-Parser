apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: "com.madvay.tools.build.gitbuildinfo"

buildscript {
    ext.kotlin_version = '1.0.5-2'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.madvay.tools.build:gitbuildinfo:0.1.5-alpha"
    }
}

sourceCompatibility = 1.7
group = "com.github.sybila.ctl"
archivesBaseName = "ctl-parser"
version = '2.1.0'

//antlr config
generateGrammarSource {
    arguments += [
            "-no-visitor",
            "-listener"
    ]
    outputDirectory = new File('src/main/gen/')
}

// make the Java compile task depend on the antlr4 task
compileKotlin.dependsOn generateGrammarSource

// add the generated source files to the list of java sources
sourceSets.main.java.srcDirs += new File('src/main/gen')


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.intellij:annotations:5.1'
    compile 'com.github.daemontus:kotlin-option-result:1.0.0'
    antlr "org.antlr:antlr4:4.5.1-1"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    //archives javadocJar, sourcesJar
    archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

buildStamp {
    // The git commit SHA will be appended to this url to generate the final url.
    repoBaseUrl "https://github.com/sybila/ctl-parser"
    // BuildInfo.java will be added under this package, in a
    // generated build/ directory.
    packageName "com.github.sybila.ctl"
}