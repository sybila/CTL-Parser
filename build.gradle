apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: "com.madvay.tools.build.gitbuildinfo"

buildscript {
    ext.kotlin_version = '1.0.0'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.madvay.tools.build:gitbuildinfo:0.1.5-alpha"
    }
}

group = "com.github.sybila"
archivesBaseName = "ctlParser"
version = '1.1.1'

//antlr config
generateGrammarSource {
    arguments += [
            "-no-visitor",
            "-listener",
            "-package", "cz.muni.fi.ctl.antlr"
    ]
    outputDirectory = new File('src/main/gen/cz/muni/fi/ctl/antlr')
}

// make the Java compile task depend on the antlr4 task
compileKotlin.dependsOn generateGrammarSource

// add the generated source files to the list of java sources
sourceSets.main.java.srcDirs += new File('src/main/gen')


repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.intellij:annotations:5.1'
    antlr "org.antlr:antlr4:4.5.1-1"
}

//create a single Jar with all dependencies
task fullJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'CTL Parser',
                'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

/* For some esoteric reason, javadoc is broken with git build info in this repository
You can try to enable this later - hopefully it's a bug in javadoc
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
*/

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    //archives javadocJar, sourcesJar
    archives sourcesJar
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

buildStamp {
    // The git commit SHA will be appended to this url to generate the final url.
    repoBaseUrl "https://github.com/sybila/ctl-parser/tree/"
    // BuildInfo.java will be added under this package, in a
    // generated build/ directory.
    packageName "cz.muni.fi.ctl"
}