//include antlr plugin
buildscript {
    ext.kotlin_version = '0.12.1218'
    repositories {
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'me.champeau.gradle.antlr4'

version = '1.0'

//antlr config
antlr4 {
    extraArgs=['-package', 'cz.muni.fi.ctl.antlr']
    output=new File('src/main/gen/cz/muni/fi/ctl/antlr')
    visitor=false
    listener=true
}

// make the Java compile task depend on the antlr4 task
compileJava.dependsOn antlr4

// add the generated source files to the list of java sources
sourceSets.main.java.srcDirs += new File('src/main/gen')

// add antlr4 to classpath
configurations {
   compile.extendsFrom antlr4
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'com.intellij:annotations:5.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

//create a single Jar with all dependencies
task fullJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'CTL Parser',
                'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}
